*&---------------------------------------------------------------------*
*& Report ZBC405_ALV_DEMO_19
*&---------------------------------------------------------------------*
*&
*&---------------------------------------------------------------------*
REPORT zbc405_alv_demo_19_e7.
TYPE-POOLS: icon.

* self define gty_sbook
TYPES: BEGIN OF gty_sbook.
    INCLUDE TYPE sbook.
TYPES: light          TYPE c LENGTH 1,
       row_color      TYPE c LENGTH 4,
       it_colfields   TYPE lvc_t_scol,
       phone          TYPE scustom-telephone,
       cancelled_icon TYPE icon-id,
       END OF gty_sbook.

* 1. Data Object
*DATA: gt_sbook TYPE TABLE OF sbook,
*      gs_sbook LIKE LINE OF gt_sbook.

* gty_sbook 용 데이터 선언
DATA: gt_sbook TYPE TABLE OF gty_sbook,
      gs_sbook TYPE gty_sbook.

* 1-1 ok_code
DATA: ok_code TYPE sy-ucomm.

* 2. Create object : container, grid_control
DATA: go_container TYPE REF TO cl_gui_custom_container,
      go_alv_grid  TYPE REF TO cl_gui_alv_grid.

* 3. Layout Variant
DATA: gv_variant TYPE disvariant,
      gv_save    TYPE c LENGTH 1.

* 4. Layout Structure
DATA: gs_layout TYPE lvc_s_layo.

* 5. Sort criteria
DATA: gt_sort TYPE lvc_t_sort,
      gs_sort TYPE lvc_s_sort.

* 6. Cell Color
DATA: gs_colfield LIKE LINE OF gs_sbook-it_colfields.

* 7. Hiding UI Functions
DATA: gt_exclude TYPE ui_functions.

* 8. Field Catalog
DATA: gt_fcat TYPE lvc_t_fcat,  " internal table 형
      gs_fcat TYPE lvc_s_fcat.  " structure 형

PARAMETERS: pa_lv TYPE disvariant-variant.

* selection-screen : carrid, connid, fldate
SELECT-OPTIONS: so_car FOR gs_sbook-carrid MEMORY ID car,
                so_con FOR gs_sbook-connid MEMORY ID con,
                so_dat FOR gs_sbook-fldate MEMORY ID dat.

AT SELECTION-SCREEN ON VALUE-REQUEST FOR pa_lv.

* 클래스 정의 부분
CLASS: lcl_handle DEFINITION.
  PUBLIC SECTION.
    CLASS-METHODS: on_double_click
                  FOR EVENT double_click OF cl_gui_alv_grid
      IMPORTING es_row_no,

      on_toolbar
                    FOR EVENT toolbar OF cl_gui_alv_grid
        IMPORTING e_object,

      on_user_command
                    FOR EVENT user_command OF cl_gui_alv_grid
        IMPORTING e_ucomm.

ENDCLASS.

* 클래스 구현 부분
CLASS: lcl_handle IMPLEMENTATION.

  METHOD on_double_click.
    DATA: iv_form    TYPE scustom-form,
          iv_name    TYPE scustom-name,
          iv_city    TYPE scustom-city,
          iv_message TYPE c LENGTH 100.

    READ TABLE gt_sbook INTO gs_sbook INDEX es_row_no-row_id.

    SELECT SINGLE form name city
      FROM scustom INTO ( iv_form, iv_name, iv_city )
      WHERE id = gs_sbook-customid.

    CONCATENATE iv_form iv_name iv_city INTO iv_message SEPARATED BY ' '.

    MESSAGE iv_message TYPE 'I'.

  ENDMETHOD.
  METHOD on_toolbar.
    DATA: ls_button TYPE stb_button.

    " Seperater
    ls_button-butn_type = 3.
    INSERT ls_button INTO TABLE e_object->mt_toolbar.

    " User define
    CLEAR ls_button.
    ls_button-function = 'DISP'.
    ls_button-icon = icon_flight.
    ls_button-quickinfo = 'Display booking information'.
    ls_button-butn_type = 0.
    INSERT ls_button INTO TABLE e_object->mt_toolbar.


  ENDMETHOD.
  METHOD on_user_command.
    DATA: lv_row TYPE i,
          ls_scarr type scarr.

    CASE e_ucomm.
      WHEN 'DISP'.
        go_alv_grid->get_current_cell(
        IMPORTING e_row = lv_row
        ).

        READ TABLE gt_sbook INTO gs_sbook INDEX lv_row.

        SELECT SINGLE *
          FROM scarr INTO CORRESPONDING FIELDS OF ls_scarr
          WHERE carrid = gs_sbook-carrid.

          CHECK sy-subrc = 0. " 정상 작동하면
            MESSAGE ls_scarr-carrname TYPE 'I'.

    ENDCASE.
  ENDMETHOD.

ENDCLASS.

* start selection
START-OF-SELECTION.

  SELECT *
    FROM sbook INTO CORRESPONDING FIELDS OF TABLE gt_sbook
    WHERE carrid IN so_car
    AND   connid IN so_con
    AND   fldate IN so_dat.

  LOOP AT gt_sbook INTO gs_sbook.
    SELECT SINGLE telephone
      FROM scustom INTO gs_sbook-phone
      WHERE id = gs_sbook-customid.               " scustom에서 gs_sbook-phone 필드에 데이터 넣기

    IF gs_sbook-cancelled = 'X'.
      gs_sbook-cancelled_icon = icon_cancel.       " icon_cancel 아이콘 넣기
    ENDIF.

    IF gs_sbook-smoker = 'X'.
      gs_colfield-fname = 'SMOKER'.
      gs_colfield-color-col = col_negative.
      gs_colfield-color-int = 1.
      gs_colfield-color-inv = 0.
      APPEND gs_colfield TO gs_sbook-it_colfields.
    ENDIF.

*    MODIFY gt_sbook FROM gs_sbook TRANSPORTING light it_colfields.

    IF gs_sbook-class = 'C'.
      gs_sbook-row_color = 'C710'.
    ENDIF.

*    MODIFY gt_sbook FROM gs_sbook TRANSPORTING light row_color.

    IF gs_sbook-luggweight > 20.
      gs_sbook-light = '1'.                 " Red
    ELSEIF gs_sbook-luggweight > 15.
      gs_sbook-light = '2'.                 " Yellow
    ELSE.
      gs_sbook-light = '3'.                 " Green
    ENDIF.

    MODIFY gt_sbook FROM gs_sbook TRANSPORTING light
                                               row_color
                                               it_colfields
                                               phone
                                               cancelled_icon.

  ENDLOOP.

  CALL SCREEN 100.
*&---------------------------------------------------------------------*
*& Module STATUS_0100 OUTPUT
*&---------------------------------------------------------------------*
*&
*&---------------------------------------------------------------------*
MODULE status_0100 OUTPUT.
  SET PF-STATUS 'S0100'.
  SET TITLEBAR 'T0100'.
ENDMODULE.
*&---------------------------------------------------------------------*
*&      Module  USER_COMMAND_0100  INPUT
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
MODULE user_command_0100 INPUT.
  CASE ok_code.
    WHEN 'BACK'.
      LEAVE TO SCREEN 0.
    WHEN 'ENTER'.
  ENDCASE.

ENDMODULE.
*&---------------------------------------------------------------------*
*& Module CLEAR_OK_CODE OUTPUT
*&---------------------------------------------------------------------*
*&
*&---------------------------------------------------------------------*
MODULE clear_ok_code OUTPUT.
  CLEAR ok_code.
ENDMODULE.
*&---------------------------------------------------------------------*
*&      Module  EXIT  INPUT
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
MODULE exit INPUT.
  CASE ok_code.
    WHEN 'EXIT'.
      LEAVE PROGRAM.
    WHEN 'CANC'.
      " clear
      LEAVE TO SCREEN 0.
  ENDCASE.

ENDMODULE.
*&---------------------------------------------------------------------*
*& Module CREATE_AND_TRANSFER OUTPUT
*&---------------------------------------------------------------------*
*&
*&---------------------------------------------------------------------*
MODULE create_and_transfer OUTPUT.
  CREATE OBJECT go_container
    EXPORTING
      container_name = 'MY_CONTROL_AREA'
    EXCEPTIONS
      OTHERS         = 1.

  IF sy-subrc <> 0.
    MESSAGE a001(zbc405_19).

  ENDIF.

  CREATE OBJECT go_alv_grid
    EXPORTING
      i_parent = go_container
    EXCEPTIONS
      OTHERS   = 1.

  IF sy-subrc <> 0.
    MESSAGE a001(zbc405_19).
  ENDIF.
* 그리드 생성 후 하고싶은 거 수행

  " Handler 설정 - 해당 핸들러 메소드 실행
  SET HANDLER lcl_handle=>on_double_click FOR go_alv_grid.
  SET HANDLER lcl_handle=>on_toolbar FOR go_alv_grid.
  SET HANDLER lcl_handle=>on_user_command FOR go_alv_grid.

* List Data
* Global Field Catalog
* 2 Layout variant
  gv_variant-report = sy-cprog.
  gv_variant-variant = pa_lv.
  gv_save = 'A'. " U, X, A "

* 3 Layout Structure
  gs_layout-grid_title = 'Booking'.
  gs_layout-zebra      = 'X'.             " 실행 시킨다는 의미의 X(체크 표시) 색깔 번갈아서
  gs_layout-cwidth_opt = 'X'.             " 실행 시킨다는 의미의 X(체크 표시) 크기 정렬  5권 p.154
  gs_layout-sel_mode   = 'C'.             " 셀모드 설정 A, B, C, D 5권 p.155
  gs_layout-excp_fname = 'LIGHT'.         " 신호등
  gs_layout-excp_led   = 'X'.             " 해당 light만 나오게 하는 설정
  gs_layout-info_fname = 'ROW_COLOR'.     " Row_color 변경
  gs_layout-ctab_fname = 'IT_COLFIELDS'.  " Cell color 변경

* 4 Sort Structure
  gs_sort-fieldname = 'FLDATE'.
  gs_sort-down = 'X'.
* 5 gs_sort-spos = 1.
  APPEND gs_sort TO gt_sort.

  CLEAR gs_sort.
  gs_sort-fieldname = 'CUSTOMID'.
  gs_sort-up        = 'X'.
* 6 gs_sort-spos      = 2.

  APPEND gs_sort TO gt_sort.
  APPEND cl_gui_alv_grid=>mc_fc_filter TO gt_exclude.     " 필터 툴바 숨기기
  APPEND cl_gui_alv_grid=>mc_mb_sum TO gt_exclude.        " sum 툴바 숨기기

* 7 Field Catalog
  gs_fcat-fieldname = 'PHONE'.                  " 필드
  gs_fcat-ref_field = 'TELEPHONE'.              " 참조 필드 설정
  gs_fcat-ref_table = 'SCUSTOM'.                " 참조 테이블 설정
  gs_fcat-coltext   = 'CellPhone'.              " 필드 이름(텍스트) 변경
  gs_fcat-col_pos   = 4.                        " 필드 위치 설정
  APPEND gs_fcat TO gt_fcat.

  CLEAR gs_fcat.
  gs_fcat-fieldname = 'CANCELLED_ICON'.         " 필드
  gs_fcat-icon = 'X'.                           " 아이콘 모양 설정
  gs_fcat-coltext = 'Cancelled'.                " 필드 이름(텍스트) 변경
  gs_fcat-col_pos = 5.                          " 필드 위치 설정
  APPEND gs_fcat TO gt_fcat.

  CLEAR gs_fcat.
  gs_fcat-fieldname = 'CANCELLED'.              " 필드
  gs_fcat-no_out    = 'X'.                      " 해당 필드 숨김
  APPEND gs_fcat TO gt_fcat.




  CALL METHOD go_alv_grid->set_table_for_first_display
    EXPORTING
      i_structure_name     = 'SBOOK'
      is_variant           = gv_variant     " Layout Variant
      i_save               = gv_save
      i_default            = 'X'
      is_layout            = gs_layout      " Layout Structure
      it_toolbar_excluding = gt_exclude     " exclude toolbar 툴바 숨기기
    CHANGING
      it_outtab            = gt_sbook
      it_sort              = gt_sort
      it_fieldcatalog      = gt_fcat
    EXCEPTIONS
      OTHERS               = 1.

  IF sy-subrc <> 0.
    "  MESSAGE

  ENDIF.


ENDMODULE.

*GUI Texts
*----------------------------------------------------------
* T0100 --> ALV Demo


*Messages
*----------------------------------------------------------
*
* Message class: ZBC405_19
*001   Container Instance is not Data

----------------------------------------------------------------------------------
Extracted by Mass Download version 1.5.5 - E.G.Mellodew. 1998-2023. Sap Release 751
