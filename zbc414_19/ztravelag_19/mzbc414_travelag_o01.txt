*----------------------------------------------------------------------*
***INCLUDE BC414_SQL_TRAVELAG_SO01
*----------------------------------------------------------------------*

*&---------------------------------------------------------------------*
*&      Module  CLEAR_OKCODE  OUTPUT
*&---------------------------------------------------------------------*
MODULE clear_okcode OUTPUT.

  CLEAR ok_code.

ENDMODULE.                 " CLEAR_OKCODE  OUTPUT


*&---------------------------------------------------------------------*
*&      Module  GET_DATA  OUTPUT
*&---------------------------------------------------------------------*
MODULE get_data OUTPUT.

  IF gv_sel_changed = abap_true.
    PERFORM get_selected_lines USING    gt_travelag
                               CHANGING gt_travelag_sel.
    gv_sel_changed = abap_false.
*   Show screen in modal dialog box.
    REFRESH CONTROL 'TC_STRAVELAG' FROM SCREEN '0200'.
  ENDIF.

ENDMODULE.                 " GET_DATA  OUTPUT


*&---------------------------------------------------------------------*
*&      Module  INIT_ALV  OUTPUT
*&---------------------------------------------------------------------*
MODULE init_alv OUTPUT.

  IF go_container IS INITIAL.
    CREATE OBJECT go_container              " 컨테이너 생성
      EXPORTING
        container_name = 'CONTAINER'.
    CREATE OBJECT go_alv                    " 그리드 생성
      EXPORTING
        i_parent = go_container.
    gv_refresh = abap_true.                 " 처음 만들어 진다면 트루로 체크
  ENDIF.
  IF gv_refresh = abap_true.                " 트루로 체크가 되있다면 => alv가 만들어 졌다면

    PERFORM read_data CHANGING gt_travelag. " ZSTRAVELAG19 테이블로 부터 데이터 읽기

    gs_layout-sel_mode = 'A'.               " 셀 복수 선택 설정 (셀모드)
    gs_layout-cwidth_opt = 'X'.             " 필드 크기 정렬    (cwidth_opt)

    CLEAR gs_sort.                          " 여기 내가 따로 넣은거
    gs_sort-fieldname = 'AGENCYNUM'.        " AGENCYNUM로 오름차순 정렬을 위한 Sort 구문
    gs_sort-up        = 'X'.
    APPEND gs_sort TO gt_sort.

    go_alv->set_table_for_first_display(    " 디스플레이 설정
      EXPORTING
        is_layout        = gs_layout
        i_structure_name = 'ZSTRAVELAG19'   " 스트럭쳐 이름 내가 복사한 테이블로 변경함 ( 기존 스탠다드 테이블 STRVELAG 였음 )
      CHANGING
        it_outtab        = gt_travelag
        it_sort          = gt_sort ).       " Sort 사용을 위해 추가함
    gv_refresh = abap_false.                " 한번 생성 이후 다시 불러오지 않도록 false로 체크 해줌

  ELSE.                                     " 이후 데이터 변경하는 것은 밑에 새로고침해주는 구문
    go_alv->refresh_table_display( ).       " 바뀐 리스트 데이터만 새로고침 해줌
  ENDIF.

ENDMODULE.                 " INIT_ALV  OUTPUT


*&---------------------------------------------------------------------*
*&      Module  STATUS_0100  OUTPUT
*&---------------------------------------------------------------------*
MODULE status_0100 OUTPUT.

  SET PF-STATUS 'DYN_100'.
  SET TITLEBAR 'DYN_100'.

ENDMODULE.                             " STATUS_0100  OUTPUT


*&---------------------------------------------------------------------*
*&      Module  STATUS_0200  OUTPUT
*&---------------------------------------------------------------------*
MODULE status_0200 OUTPUT.

  SET PF-STATUS 'DYN_200'.
  SET TITLEBAR 'DYN_200'.

ENDMODULE.                             " STATUS_0200  OUTPUT


*&---------------------------------------------------------------------*
*&      Module  TRANS_TO_0200  OUTPUT
*&---------------------------------------------------------------------*
MODULE trans_to_0200 OUTPUT.

* Field transport to screen
  MOVE-CORRESPONDING gs_travelag TO zstravelag19.

ENDMODULE.                             " TRANS_TO_0200  OUTPUT

----------------------------------------------------------------------------------
Extracted by Mass Download version 1.5.5 - E.G.Mellodew. 1998-2023. Sap Release 751
